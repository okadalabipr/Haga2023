#=============================================================================80
# Fit the steady state model of endogenous THBS1, FMOD, and TGFb1 expressions
# to the experimental data (Fig. 2A, B).
#
# Author: Keita Iida (Haga, Iida, Okada, 2023)
#=============================================================================80

reset

#-----------------------------------------------------------------------------80
# Filename
#-----------------------------------------------------------------------------80
data = 'data/Fig2AB/PDL_TGFb_THBS1_FMOD.txt'

#-----------------------------------------------------------------------------80
# Define the equations.
# * TGFb1: x3(x)
# * THBS1: x1(x)
# * FMOD:  x2(x)
# where x is the input values of PDL.
#-----------------------------------------------------------------------------80
x1(x) = Ap * x / (x + Kap) + e1
x2(x) = Bp * Kbp / (x + Kbp) + e2
x3(x) = C * x1(x) / (x1(x) + K1) * K2 / (x2(x) + K2) + e3
#-----------------------------------------------------------------------------80
# Plotting options
#-----------------------------------------------------------------------------80
unset key
set size ratio 0.7
set border lw 3
set xlabel font "Helvetica,20"
set ylabel font "Helvetica,20" offset -0.5,0
set tics   font "Helvetica,20"
set tics scale 2
#-----------------------------------------------------------------------------80
# PDL vs THBS1
#-----------------------------------------------------------------------------80
fit x1(x) data u 2:4 via e1, Ap, Kap
set xlabel ''
set ylabel 'THBS1'
set xtics ("PDL24" 24, "PDL36" 36, "PDL47" 47, "PDL53" 53) offset 0,graph -0.01
set ytics 0.5 offset -0.5,graph 0
p [20:60][] data u 2:($1==1 ? $4 : 1/0) w p ps 3   pt 5  lc rgb 'black', \
            ''   u 2:($1==2 ? $4 : 1/0) w p ps 3   pt 7  lc rgb 'black', \
            ''   u 2:($1==3 ? $4 : 1/0) w p ps 3.7 pt 13 lc rgb 'black', \
          x1(x) w l lw 5 lc rgb 'red'
pause -1
#-----------------------------------------------------------------------------80
# PDL vs FMOD
#-----------------------------------------------------------------------------80
fit x2(x) data u 2:5 via e2, Bp, Kbp
set xlabel ''
set ylabel 'FMOD'
set xtics ("PDL24" 24, "PDL36" 36, "PDL47" 47, "PDL53" 53) offset 0,graph -0.01
set ytics 0.5 offset -0.5,graph 0
p [20:60][] data u 2:($1==1 ? $5 : 1/0) w p ps 3   pt 5  lc rgb 'black', \
            ''   u 2:($1==2 ? $5 : 1/0) w p ps 3   pt 7  lc rgb 'black', \
            ''   u 2:($1==3 ? $5 : 1/0) w p ps 3.7 pt 13 lc rgb 'black', \
          x2(x) w l lw 5 lc rgb 'red'
pause -1
#-----------------------------------------------------------------------------80
# PDL vs TGFb1
#-----------------------------------------------------------------------------80
fit x3(x) data u 2:3 via e3, C, K1, K2
set xlabel ''
set ylabel 'TGFb1'
set xtics ("PDL24" 24, "PDL36" 36, "PDL47" 47, "PDL53" 53) offset 0,graph -0.01
set ytics 0.5 offset -0.5,graph 0
p [20:60][0.5:2] data u 2:($1==1 ? $3 : 1/0) w p ps 3   pt 5  lc rgb 'black', \
            ''        u 2:($1==2 ? $3 : 1/0) w p ps 3   pt 7  lc rgb 'black', \
            ''        u 2:($1==3 ? $3 : 1/0) w p ps 3.7 pt 13 lc rgb 'black', \
          x3(x) w l lw 5 lc rgb 'red'
pause -1

