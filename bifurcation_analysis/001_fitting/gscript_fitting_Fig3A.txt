#=============================================================================80
# Fit the steady state model of THBS1, FMOD, and TGFb1 expressions to the
# experimental data (Fig. 3A).
#
# Author: Keita Iida (Haga, Iida, Okada, 2023)
#=============================================================================80

reset

#-----------------------------------------------------------------------------80
# Filename
#-----------------------------------------------------------------------------80
data = 'data/Fig3A/TGFb_THBS1_FMOD.txt'

#-----------------------------------------------------------------------------80
# Define the equations.
# * THBS1: x1(x) = A * x**na / (x**na + Ka) + e1
# * FMOD:  x2(x) = B * Kb / (x**nb + Kb) + e2
# where x is the input values of TGFb1.
#-----------------------------------------------------------------------------80
x1(x) = A * x**na / (x**na + Ka) + e1
x2(x) = B * Kb / (x**nb + Kb) + e2
#-----------------------------------------------------------------------------80
# Plotting options
#-----------------------------------------------------------------------------80
unset key
set size ratio 0.7
set border lw 3
set xlabel font "Helvetica,20"
set ylabel font "Helvetica,20"
set tics   font "Helvetica,20"
set tics scale 2
#-----------------------------------------------------------------------------80
# For log-transformation of x-axis
#-----------------------------------------------------------------------------80
eps = 1
set logscale x 10
#-----------------------------------------------------------------------------80
# TGFb1 vs THBS1
#-----------------------------------------------------------------------------80
Ka = 0.1
fit x1(x-eps) data u ($2+eps):3 via e1, A, Ka, na
set xlabel 'log_{10}(TGFb\_act + 1)'
set ylabel 'THBS1'
set ytics 1
p [0.9:10] data u ($2+eps):($1==1 ? $3 : 1/0) w p ps 3   pt 5  lc rgb 'black', \
             '' u ($2+eps):($1==2 ? $3 : 1/0) w p ps 3   pt 7  lc rgb 'black', \
             '' u ($2+eps):($1==3 ? $3 : 1/0) w p ps 3.7 pt 13 lc rgb 'black', \
           x1(x-eps) w l lw 5 lc rgb 'red'
pause -1
#-----------------------------------------------------------------------------80
# TGFb1 vs FMOD
#-----------------------------------------------------------------------------80
Kb = 0.1
fit x2(x-eps) data u ($2+eps):4 via e2, B, Kb, nb
set xlabel 'log_{10}(TGFb\_act + 1)'
set ylabel 'FMOD'
set ytics 0.2
p [0.9:10] data u ($2+eps):($1==1 ? $4 : 1/0) w p ps 3   pt 5  lc rgb 'black', \
             '' u ($2+eps):($1==2 ? $4 : 1/0) w p ps 3   pt 7  lc rgb 'black', \
             '' u ($2+eps):($1==3 ? $4 : 1/0) w p ps 3.7 pt 13 lc rgb 'black', \
           x2(x-eps) w l lw 5 lc rgb 'red'
pause -1

