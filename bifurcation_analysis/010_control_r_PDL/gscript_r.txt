#=============================================================================80
# Compute steady state solutions for THBS1, FMOD, and TGFb1 expressions, wrt
# parameters for PDL.
#
# Author: Keita Iida (Haga, Iida, Okada, 2023)
#=============================================================================80

reset

#-----------------------------------------------------------------------------80
# Filename
#-----------------------------------------------------------------------------80
data = '01_outgoing/x.txt'

#-----------------------------------------------------------------------------80
# Plotting options
#-----------------------------------------------------------------------------80
unset key
set size ratio 0.7
set border lw 3
set xlabel font "Helvetica,10"
set ylabel font "Helvetica,10"
set tics   font "Helvetica,10"
set tics scale 2
set xtics nomirror
set ytics nomirror
unset mxtics 
unset mytics 

set xlabel 'Gamma'
#-----------------------------------------------------------------------------80
# Plot
#-----------------------------------------------------------------------------80
set xtics 1
set ytics 1
set ylabel 'THBS1'
unset arrow
set arrow from 3.5,0 to 3.5,3 nohead lt -1 lw 4 dt (1,3)
p [0:4][:] \
data u ($3):($7<0 ? $4 : 1/0) w p ps 1 pt 7 lc rgb "red", \
''   u ($3):($7>0 ? $4 : 1/0) w p ps 1 pt 7 lc rgb "blue", \
0 w l lw 5.5 lc rgb "red"
pause -1
#-----------------------------------------------------------------------------80
# Plot
#-----------------------------------------------------------------------------80
set xtics 1
set ytics 0.1
set ylabel 'FMOD'
unset arrow
set arrow from 3.5,0 to 3.5,0.4 nohead lt -1 lw 4 dt (1,3)
p [0:4][:] \
data u ($3):($7<0 ? $5 : 1/0) w p ps 1 pt 7 lc rgb "red", \
''   u ($3):($7>0 ? $5 : 1/0) w p ps 1 pt 7 lc rgb "blue", \
0.33 w l lw 5.5 lc rgb "red"
pause -1
#-----------------------------------------------------------------------------80
# Plot
#-----------------------------------------------------------------------------80
set xtics 1
set ytics 1
set ylabel 'TGFb1'
unset arrow
set arrow from 3.5,0 to 3.5,4 nohead lt -1 lw 4 dt (1,3)
p [0:4][:] \
data u ($3):($7<0 ? $6 : 1/0) w p ps 1 pt 7 lc rgb "red", \
''   u ($3):($7>0 ? $6 : 1/0) w p ps 1 pt 7 lc rgb "blue", \
0 w l lw 5.5 lc rgb "red"
pause -1
