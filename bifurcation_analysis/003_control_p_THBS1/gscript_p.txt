#=============================================================================80
# Compute steady state solutions for THBS1, FMOD, and TGFb1 expressions, wrt
# parameters for THBS1 and FMOD, i.e., K1 (p) and K2 (q), respectively.
#
# M. Haga, K. Iida, M. Okada (2023)
#=============================================================================80

reset

#-----------------------------------------------------------------------------80
# Filename
#-----------------------------------------------------------------------------80
data = '01_outgoing/x.txt'

#-----------------------------------------------------------------------------80
# Plotting options
#-----------------------------------------------------------------------------80
unset key
set size ratio 0.7
set border lw 3
set xlabel font "Helvetica,10"
set ylabel font "Helvetica,10"
set tics   font "Helvetica,10"
set tics scale 2
set xtics nomirror
set ytics nomirror
unset mxtics 
unset mytics 

set xlabel 'K1'
#-----------------------------------------------------------------------------80
# Plot
#-----------------------------------------------------------------------------80
set ylabel 'THBS1'
p [10:18][-0.125:2] \
data u ($1):($6<0 ? $3 : 1/0) w p ps 1 pt 7 lc rgb "red", \
''   u ($1):($6>0 ? $3 : 1/0) w p ps 1 pt 7 lc rgb "blue", \
0 w l lw 5.5 lc rgb "red"
pause -1
#-----------------------------------------------------------------------------80
# Plot
#-----------------------------------------------------------------------------80
set ylabel 'FMOD'
p [10:18][-0.025:0.4] \
data u ($1):($6<0 ? $4 : 1/0) w p ps 1 pt 7 lc rgb "red", \
''   u ($1):($6>0 ? $4 : 1/0) w p ps 1 pt 7 lc rgb "blue", \
0.33 w l lw 5.5 lc rgb "red"
pause -1
#-----------------------------------------------------------------------------80
# Plot
#-----------------------------------------------------------------------------80
set ylabel 'TGFb1'
p [10:18][-0.01:0.16] \
data u ($1):($6<0 ? $5 : 1/0) w p ps 1 pt 7 lc rgb "red", \
''   u ($1):($6>0 ? $5 : 1/0) w p ps 1 pt 7 lc rgb "blue", \
0 w l lw 5.5 lc rgb "red"
pause -1
